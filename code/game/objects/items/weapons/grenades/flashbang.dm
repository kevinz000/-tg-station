/obj/item/weapon/grenade/flashbang
	name = "flashbang"
	icon_state = "flashbang"
	item_state = "flashbang"
	origin_tech = "materials=2;combat=3"

/obj/item/weapon/grenade/flashbang/prime()
	update_mob()
	var/flashbang_turf = get_turf(src)
	if(!flashbang_turf)
		return
	for(var/mob/living/M in get_hearers_in_view(10, flashbang_turf))
		flashbang_act(M, get_dist(flashbang_turf, M))

	for(var/obj/structure/blob/B in get_hear(8,flashbang_turf))     		//Blob damage here
		var/distance = get_dist(B, get_turf(src))
		var/damage = round(100/(distance*distance)+1)
		B.take_damage(damage, BURN, "energy")
	qdel(src)

/obj/item/weapon/grenade/flashbang/proc/flashbang_act(mob/living/L, range)
	var/s1 = max(5, ((-range)/2) + 6)
	L.show_message("<font size='[s1]' color='red'><b>BANG</font></b>", 2)
	playsound(loc, 'sound/weapons/flashbang.ogg', 100, 1)
	if(isalien(L))
		return
	if(isanimal(L))
		L.adjustFireLoss((10-range)*2)
	var/eye_protect = L.flash_act(intensity = Ceiling((10-range)/4), affect_silicon = FALSE)
	var/sound_protect = L.soundbang_act(intensity = Ceiling((10-range)/4), stun_pwr = 0, damage_pwr = Ceiling((10-range)*2), deafen_pwr = Ceiling(10-range))
	var/total_protect = min(eye_protect, sound_protect)
	if(iscarbon(L))
		var/mob/living/carbon/M = L
		switch(range)
			if(0)
				M.Weaken(10)
				M.confused = max(M.confused, 15)
				M.drowsyness = max(M.drowsyness, 10)
				M.adjustFireLoss(15)
				M.show_message("<span class='danger'>The [src] sears you!</span>")
				M.dizziness = max(M.dizziness, 10)
			if(1)
				if(total_protect > 0)
					M.Weaken(5)
					M.confused = max(M.confused, 7)
					M.drowsyness = max(M.drowsyness, 7)
					M.dizziness = max(M.dizziness, 7)
				else
					M.Weaken(10)
					M.confused = max(M.confused, 10)
					M.drowsyness = max(M.drowsyness, 10)
					M.dizziness = max(M.dizziness, 10)
				M.adjustFireLoss(10)
				M.show_message("<span class='danger'>The [src] sears you!</span>")
			if(2)
				if(total_protect > 0)
					M.Weaken(3)
					M.confused = max(M.confused, 5)
					M.drowsyness = max(M.drowsyness, 5)
				else
					M.Weaken(7)
					M.confused = max(M.confused, 9)
					M.drowsyness = max(M.drowsyness, 9)
					M.dizziness = max(M.dizziness, 9)
			if(3)
				if(total_protect > 0)
					M.flash_act(5)
				else
					M.Weaken(4)
					M.confused = max(M.confused, 6)
					M.drowsyness = max(M.drowsyness, 5)
			if(4 to 5)
				if(total_protect <= 0)
					M.Weaken(2)
					M.confused = max(M.confused, 4)
					M.drowsyness = max(M.drowsyness, 4)
			if(6 to 7)
				if(total_protect <= 0)
					M.confused = max(M.confused, 4)
					M.drowsyness = max(M.drowsyness, 2)
					M.drop_all_held_items()
	if(issilicon(L))
		var/mob/living/silicon/R = L
		switch(range)
			if(1)
				R.Weaken(10)
			if(2 to 3)
				R.Weaken(7)
			if(4 to 5)
				R.Weaken(5)
			if(6 to 7)
				R.Weaken(2)
